# -*- cperl -*-
#
# beginnings of async test script
#

#*  uses
#
use DBI;
use DBD::Pg	':async';

#*  constants
#
use constant CONNSPEC =>	sprintf('dbi:Pg:dbname=%s', $ENV{USER});

#*  subs
#
sub msg
{
    my $msg;

    $msg = sprintf($_[0], @_[1 .. $#_]);
    print STDERR ($msg, "\n");
}

sub wait_for
{
    my ($fh, $what) = @_;
    my ($rin, $win, $vec, $nf);
    
    vec($vec, fileno($fh), 1) = 1;
    if ($what == PG_ASYNC_CONN_READ) {
        $rin = $vec;
    } else {
        $win = $vec;
    }

    $nf = select($rin, $win, undef, undef);
    msg('select returned %d', $nf);
}

sub dbh_connect
{
    my ($fh, $dbh, $rc);
    
    msg('connecting');

    $dbh = DBI->connect(CONNSPEC, '', '',
                               {
                                RaiseError => 1,
                                pg_server_prepare => 1,
                                pg_async_connect => 1});
    open($fh, '<&', $$dbh{pg_socket});

    do {
        $rc = $dbh->pg_continue_async_connect();
        msg('continue async connect %d', $rc);
        wait_for($fh, $rc) if $rc;
    } while $rc;

    msg('connected');

    return $dbh;
}

sub do_query
{
    my $dbh = $_[0];
    my ($fh, $sth, $rc);

    $sth = $dbh->prepare('select * from the_tavel', { pg_sync => 1 });
    $sth->execute();

    open($fh, '<&', $$dbh{pg_socket});

    do {
        wait_for($fh, PG_ASYNC_CONN_READ);
        $rc = $dbh->pg_ready();
    } until $rc;

    $dbh->pg_result();
    return $sth;
}

#*  main
#
{
    my ($dbh, $sth, $row);
    
#    DBI->trace(15);
    $dbh = dbh_connect();
    $sth = do_query($dbh);
    while ($row = $sth->fetchrow_arrayref()) {
        print("@$row", "\n");
    }
}
